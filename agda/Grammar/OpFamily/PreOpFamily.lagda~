\begin{code}
open import Grammar.Base

module Grammar.OpFamily.PreOpFamily (G : Grammar) where
open import Prelims.EqReasoning
open Grammar G
\end{code}

%<*PreOpFamily>
\begin{code}
record PreOpFamily : Set₂ where
  field
    Op : Alphabet → Alphabet → Set
    apV : ∀ {U} {V} {K} → Op U V → Var U K → Expression V (varKind K)
    up : ∀ {V} {K} → Op V (V , K)
    apV-up : ∀ {V} {K} {L} {x : Var V K} → apV (up {K = L}) x ≡ var (↑ x)
    idOp : ∀ V → Op V V
    apV-idOp : ∀ {V} {K} (x : Var V K) → apV (idOp V) x ≡ var x
\end{code}
%</PreOpFamily>

%<*EqualOp>
\begin{code}
  _∼op_ : ∀ {U} {V} → Op U V → Op U V → Set
  _∼op_ {U} {V} ρ σ = ∀ {K} (x : Var U K) → apV ρ x ≡ apV σ x

  ∼-refl : ∀ {U} {V} {σ : Op U V} → σ ∼op σ
\end{code}

\AgdaHide{
\begin{code}
  ∼-refl _ = refl
\end{code}
}

\begin{code}
  ∼-sym : ∀ {U} {V} {σ τ : Op U V} → σ ∼op τ → τ ∼op σ
\end{code}

\AgdaHide{
\begin{code}
  ∼-sym σ-is-τ x = ≡-sym (σ-is-τ x)
\end{code}
}

\begin{code}
  ∼-trans : ∀ {U} {V} {ρ σ τ : Op U V} → ρ ∼op σ → σ ∼op τ → ρ ∼op τ
\end{code}
%</EqualOp>

\AgdaHide{
\begin{code}
  ∼-trans ρ-is-σ σ-is-τ x = ≡-trans (ρ-is-σ x) (σ-is-τ x)

  OP : Alphabet → Alphabet → Setoid _ _
  OP U V = record { 
     Carrier = Op U V ; 
     _≈_ = _∼op_ ; 
     isEquivalence = record { 
       refl = ∼-refl ; 
       sym = ∼-sym ; 
       trans = ∼-trans } }
\end{code}
}
