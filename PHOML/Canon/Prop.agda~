module PHOML.Canon.Prop where

data CanonProp : Set where
  bot : CanonProp
  imp : CanonProp → CanonProp → CanonProp

decode : ∀ {V} → CanonProp → Term V
decode bot = ⊥
decode (imp φ ψ) = decode φ ⊃ decode ψ

canon-nf : ∀ {V θ} {φ : Term V} → decode θ ⇒ φ → Empty
canon-nf {θ = bot} ()
canon-nf {θ = imp φ _} (impl θ⇒φ) = canon-nf {θ = φ} θ⇒φ
canon-nf {θ = imp _ ψ} (impr θ⇒φ) = canon-nf {θ = ψ} θ⇒φ

canon-nf' : ∀ {V} θ {φ ψ : Term V} → φ ↠ ψ → decode θ ≡ φ → decode θ ≡ ψ
canon-nf' θ (inc φ⇒ψ) θ≡φ = ⊥-elim (canon-nf {θ = θ} (subst (λ x → x ⇒ _) (≡-sym θ≡φ) φ⇒ψ))
canon-nf' _ ref θ≡φ = θ≡φ
canon-nf' θ (trans φ↠ψ ψ↠ψ') θ≡φ = canon-nf' θ ψ↠ψ' (canon-nf' θ φ↠ψ θ≡φ)

red-canon : ∀ {V} {φ ψ : Term V} {θ : CanonProp} → φ ↠ decode θ → φ ≃ ψ → ψ ↠ decode θ
red-canon {V} {φ} {ψ} {θ} φ↠θ φ≃ψ = 
  let cr χ θ↠χ ψ↠χ = diamond-CR (λ _ _ _ → diamond) (decode θ) ψ (trans (sym (sub-RT-RST φ↠θ)) φ≃ψ) in 
  subst (λ x → ψ ↠ x) (≡-sym (canon-nf' θ θ↠χ refl)) ψ↠χ

imp-red-imp : ∀ {V} {E F : Term V} → E ↠ F → Is-⊃ E → Is-⊃ F
imp-red-imp (inc (impl _)) is-⊃ = is-⊃
imp-red-imp (inc (impr _)) is-⊃ = is-⊃
imp-red-imp ref is⊃E = is⊃E
imp-red-imp (trans E↠F F↠G) is⊃E = imp-red-imp F↠G (imp-red-imp E↠F is⊃E)

imp-not-red-bot : ∀ {V} {φ ψ : Term V} → φ ⊃ ψ ↠ ⊥ → Empty
imp-not-red-bot φ⊃ψ↠⊥ with imp-red-imp φ⊃ψ↠⊥ is-⊃
imp-not-red-bot φ⊃ψ↠⊥ | ()

APPl-red-canon : ∀ {V M N} {NN : snocList (Term V)} {θ} → APPl (appT M N) NN ↠ decode θ → Reduces-to-Λ M
APPl-red-canon {NN = NN} Mx↠θ with APPl-red NN Mx↠θ refl
APPl-red-canon {NN = []} {θ = bot} _ | inj₁ (_ ,p _ ,p ())
APPl-red-canon {NN = _ snoc _} {θ = bot} _ | inj₁ (_ ,p _ ,p ())
APPl-red-canon {NN = []} {θ = imp _ _} _ | inj₁ (_ ,p _ ,p ())
APPl-red-canon {NN = _ snoc _} {θ = imp _ _} _ | inj₁ (_ ,p _ ,p ())
APPl-red-canon _ | inj₂ MrtΛ = MrtΛ

θps-red-ref : ∀ {V} (θ : CanonProp) → decode θ ⟦⟦ refSub ∶ idSub V ≡ idSub _ ⟧⟧ ↠ reff (decode θ)
θps-red-ref bot = ref
θps-red-ref (imp θ θ') = trans (↠-imp* (θps-red-ref θ) (θps-red-ref θ')) (inc ref⊃*)

